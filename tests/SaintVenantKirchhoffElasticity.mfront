@DSL DefaultFiniteStrainDSL;
@Behaviour SaintVenantKirchhoffElasticity;
@Author T. Helfer;
@Date 22/12/2020;

@Parameter young = 70.e9;
young.setGlossaryName("YoungModulus");
@Parameter nu = 0.34;
nu.setGlossaryName("PoissonRatio");

@LocalVariable stress lambda;
@LocalVariable stress mu;

@StateVariable real eeq;
eeq.setGlossaryName("EquivalentStrain");

@InitLocalVariables{
  lambda = computeLambda(young,nu);
  mu     = computeMu(young,nu);
}

@PredictionOperator<DS_DEGL>{
  static_cast<void>(smt);
  Dt = lambda*Stensor4::IxI()+2*mu*Stensor4::Id();
}

@Integrator{
  const auto e = computeGreenLagrangeTensor(F1);
  const auto s = lambda * trace(e) * StrainStensor::Id() + 2 * mu * e;
  sig = convertSecondPiolaKirchhoffStressToCauchyStress(s,F1);
  eeq = (e | e);
}

@TangentOperator<DS_DEGL>{
  static_cast<void>(smt);
  Dt = lambda * Stensor4::IxI() + 2 * mu * Stensor4::Id();
}
